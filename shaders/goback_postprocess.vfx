HEADER
{
    CompileTargets = ( IS_SM_50 && ( PC || VULKAN ) );
    Description = "Go back - Post Processing Shader";
}

MODES
{
    Default();
    VrForward();
}

FEATURES
{
    Feature( F_TINT, 0..1, "Rendering" );
}

COMMON
{
    #include "postprocess/shared.hlsl"
}

struct VertexInput
{
    float3 vPositionOs : POSITION < Semantic( PosXyz ); >;
    float2 vTexCoord : TEXCOORD0 < Semantic( LowPrecisionUv ); >;
};

struct PixelInput
{
    float2 vTexCoord : TEXCOORD0;

	// VS only
	#if ( PROGRAM == VFX_PROGRAM_VS )
		float4 vPositionPs		: SV_Position;
	#endif

	// PS only
	#if ( ( PROGRAM == VFX_PROGRAM_PS ) )
		float4 vPositionSs		: SV_ScreenPosition;
	#endif
};

VS
{
    PixelInput MainVs( VertexInput i )
    {
        PixelInput o;
        o.vPositionPs = float4(i.vPositionOs.xyz, 1.0f);
        o.vTexCoord = i.vTexCoord;
        return o;
    }
}

PS
{
    #include "postprocess/common.hlsl"

    StaticCombo( S_TINT, F_TINT, Sys( PC ) );

    RenderState( DepthWriteEnable, false );
    RenderState( DepthEnable, false );

    CreateTexture2D( g_tColorBuffer ) < Attribute( "ColorBuffer" );  	SrgbRead( true ); Filter( BILINEAR ); AddressU( CLAMP ); AddressV( CLAMP ); >;
    CreateTexture2D( g_tDownscaled ) < Attribute( "DownscaleBuffer" ); 	SrgbRead( true ); Filter( POINT ); AddressU( CLAMP ); AddressV( CLAMP ); >;

    CreateInputTexture2D( TextureBayer, Linear, 8, "", "_bayer", "Bayer Matrix", Default( 0.0f ) );
    CreateTexture2DWithoutSampler( g_tBayerMatrix )< Channel( R, None( TextureBayer ), Linear ); OutputFormat( RGBA8888 ); SrgbRead( false ); >;

    DynamicCombo( D_PASS, 0..1, Sys( PC ) );

    #if D_PASS == 0
        #define PS_DownscalePass MainPs
    #elif D_PASS == 1
        #define PS_UpscaleDitherPass MainPs
    #endif

    #if S_TINT
        float3 g_vTintColor< Default3(1.0f, 1.0f, 1.0f); UiType( Color ); UiGroup( "Go Back Settings,10/10" ); >;
    #endif

    float4 g_vBayerMatrixDim < Source( TextureDim ); SourceArg( g_tBayerMatrix ); >;
    
    SamplerState g_sPointWrap < Filter( POINT ); AddressU( WRAP ); AddressV( WRAP ); >;

    float2 g_vInternalResolution< Attribute("InternalResolution"); >;
    int g_ColorDepth< Default(32); Range(0, 8); UiGroup( "Go Back Settings,10/10" ); >;
    int g_DitherScale< Default(1.0f); Range(1.0f, 16.0f); UiGroup( "Go Back Settings,10/10" ); >;

    struct PixelOutput
    {
        float4 vColor : SV_Target0;
    };

    // Convert from RGB to YUV
    float3 RgbToYuv(float3 vColor)
    {
        // Some magic constants to help with our conversion
        const float3 vYuv0 = float3(0.299f, 0.587f, 0.114f);
        const float3 vYuv1 = float3(-0.147f, -0.289f, 0.436f);
        const float3 vYuv2 = float3(0.615f, -0.515f, -0.1f);
        
        return float3(
            dot(vColor, vYuv0),
            dot(vColor, vYuv1) + 0.5f,
            dot(vColor, vYuv2) + 0.5f
        );
    }

    // YUV to RGB
    float3 YuvToRgb(float3 vColor)
    {
        const float3 vRgb0 = float3(1.0f, 0.0f, 1.4f);
        const float3 vRgb1 = float3(1.0f, -0.395f, -0.581f);
        const float3 vRgb2 = float3(1.0f, 2.032f, 0.0f);
        vColor.gb -= 0.5f;
        return float3(
            dot(vColor, vRgb0),
            dot(vColor, vRgb1),
            dot(vColor, vRgb2)
        );
    }

    // Dither our colors
    float3 DecodeDither(float3 vColorError, float2 vUvs)
    {
        // Get our current dither cell
        float flLimit = Tex2DLevelS( g_tBayerMatrix, g_sPointWrap, vUvs, 0 ).r;

        // Dither!!!
        return step(flLimit, vColorError);
    }

    // Downscale pass
    PixelOutput PS_DownscalePass( PixelInput i )
    {
        float2 vScreenUv = i.vTexCoord.xy;

        float3 vFrameBufferColor = Tex2DLevel( g_tColorBuffer, vScreenUv.xy, 0 ).rgb;
        
        PixelOutput o;
        o.vColor.rgb = vFrameBufferColor;
        o.vColor.a = 1.0f;
        return o;
    }


    PixelOutput PS_UpscaleDitherPass( PixelInput i )
    {
        PixelOutput o;
        o.vColor.a = 1.0f;

        float2 vScreenUv = i.vTexCoord.xy;
        float3 vFrameBufferColor = Tex2DLevel( g_tDownscaled, vScreenUv.xy, 0 ).rgb;

        #if S_TINT
            vFrameBufferColor = dot(vFrameBufferColor, float3(0.299, 0.587, 0.114));
        #endif

         // RGB -> YUV
        float3 vYUVColor = RgbToYuv(vFrameBufferColor.rgb);
        
        // Get our desired color depth
        float flDepthBits = pow(2.0f, (float)g_ColorDepth);

        // Get the upper and lower color of our color depth
        float3 vYuvFloor = floor(vYUVColor * (float)flDepthBits) / (float)flDepthBits;
        float3 vYuvCeil = ceil(vYUVColor * (float)flDepthBits) / (float)flDepthBits;
        
        // Fill our screen with dithering blocks

        float flAspect = (float)g_vRenderTargetSize.x / (float)g_vRenderTargetSize.y;
        float flInternalAspect = (float)g_vInternalResolution.x / (float)g_vInternalResolution.y;
        float flAspectDiff = flInternalAspect / flAspect;

        float2 vBlocksToFit = g_vInternalResolution / g_vBayerMatrixDim.xy;
        float2 vDitherBlock = vScreenUv * (vBlocksToFit * float2(1.0f, flAspectDiff));
        
        // Build our dither table
        float3 vDithered = DecodeDither(vYUVColor, vDitherBlock.xy);
        
        // Dither -> desired color and convert from YUV -> RGB
        o.vColor.rgb = YuvToRgb(lerp(
            vYuvFloor,
            vYuvCeil,
            vDithered
        ));
        
        // If we were tinting, we're in black and white. Lets tint our output
        #if S_TINT
            o.vColor.rgb = dot(o.vColor.rgb, float3(0.299, 0.587, 0.114)) * g_vTintColor;
        #endif

        return o;
    }
}
